
EmailEvents
| extend auth = parse_json(AuthenticationDetails)
| extend SPF   = tolower(tostring(auth.SPF)),
         DKIM  = tolower(tostring(auth.DKIM)),
         DMARC = tolower(tostring(auth.DMARC))
| where SPF == "fail" or DKIM == "fail" or DMARC == "fail"
| project Timestamp, SenderFromDomain, SenderIPv4, Subject, SPF, DKIM, DMARC, DeliveryAction, NetworkMessageId
| order by Timestamp desc

//following dmarc
EmailEvents
| extend auth = parse_json(AuthenticationDetails)
| extend SPF   = tolower(tostring(auth.SPF)),
         DKIM  = tolower(tostring(auth.DKIM)),
         DMARC = tolower(tostring(auth.DMARC))
| summarize
    SPF_pass   = countif(SPF  == "pass"),
    SPF_fail   = countif(SPF  == "fail"),
    DKIM_pass  = countif(DKIM == "pass"),
    DKIM_fail  = countif(DKIM == "fail"),
    DMARC_pass = countif(DMARC== "pass"),
    DMARC_fail = countif(DMARC== "fail")
  by bin(Timestamp, 1d)
| order by Timestamp asc


// who fails dmarc
EmailEvents
| extend auth = parse_json(AuthenticationDetails)
| extend DMARC = tolower(tostring(auth.DMARC))
| where DMARC == "fail"
| summarize EchecsDMARC = count() by tostring(SenderFromDomain)
| top 10 by EchecsDMARC desc


//dmarc and deliverability
EmailEvents
| where EmailDirection == "Inbound"
| where Timestamp > ago(14d)
| where isnotempty(SenderFromDomain)
| extend auth = parse_json(AuthenticationDetails)
| extend DMARC = tolower(coalesce(tostring(auth.DMARC.Result), tostring(auth.DMARC)))
| where DMARC == "fail"
| summarize
    Total       = count(),
    Delivered   = countif(DeliveryAction == "Delivered"),
    Blocked     = countif(DeliveryAction == "Blocked"),
    Quarantined = countif(DeliveryLocation == "Quarantine")
  by SenderFromDomain
| top 15 by Total desc
